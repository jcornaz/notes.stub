buildscript {
    ext.api_version = '0.1'

    ext.kotlin_version = '1.0.2'
    ext.dokka_version = '0.9.8'
	ext.gradle_git_version = '1.5.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
		classpath "org.ajoberstar:gradle-git:$gradle_git_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.ajoberstar.github-pages'

project.group = 'com.toolable'
project.version = '0.1.2'

sourceCompatibility = 1.6
targetCompatibility = 1.6

javadoc.title = "Notes Stub " + project.version


jar {
    manifest {
        attributes 'Implementation-Title': 'Notes Stub', 'Implementation-Version': project.version
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // IBM Copyrighted dependence
    compile files('libs/Notes.jar')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.google.guava:guava:19.0'
    compile 'joda-time:joda-time:2.9.4'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dokka {
    moduleName = "notesstub"
    includes = ['packages.md']
}

githubPages {
    pages {
        from dokka
        into "doc/${api_version}"
    }
}

check.dependsOn jacocoTestReport